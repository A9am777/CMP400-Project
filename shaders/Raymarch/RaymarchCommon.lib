#ifndef RAYMARCHCOMMON_H
#define RAYMARCHCOMMON_H
#include "../Utility/Globals.lib"
#include "../Utility/MeshCommon.lib"

struct MarchVolumeDispatchInfo
{
  float outputHorizontalStep; // View step per horizontal thread
  float outputVerticalStep; // View step per vertical thread
  float initialZStep; // Distance to initially jump in Z
  float marchZStep; // Distance to typically jump in Z
 
  uint iterations; // Number of volume steps to take
  uint flagManualMarch; // Whether to attempt to automatically adjust raymarching or use the manual values
  float2 padding;
};

struct BasicOptics
{
  float4 anisotropicForwardTerms; // Forward anisotropic parameters per major light component
  float4 anisotropicBackwardTerms; // Backward anisotropic parameters per major light component
  float4 phaseBlendWeightTerms; // Per component phase blend factor
  
  float scatterAngstromExponent;
  float absorptionAngstromExponent;
  float attenuationFactor; // Scales optical depth
  float powderCoefficient; // Beers-Powder scaling factor
  float4x4 spectralWavelengths; // Wavelengths to integrate over
  float4x4 spectralWeights; // Spectral integration weights
  
  float4 ambientFraction;
  uint4 flags;
};

struct MarchParams
{
  float initialStep;
  float marchZStep;
  uint iterations;
  bool mask;
};

struct Ray
{
  float4 pos;
  float4 dir;
  float4 colour;
};

struct Sphere
{
  float4 pos;
  float sqrRadius;
};

struct Cube
{
  float4 pos;
  float3 size;
};

struct RectangleIntegrator // Midpoint rule
{
  float total;
  uint count;
};

struct SimpsonsIntegrator // Simpsons rule
{
  float firstTerm;
  float lastTerm;
  float odds;
  float evens;
  uint count;
};

static float PI = radians(180.);

// The depth to find the ray 'end' at
#define ZDirectionTest .01


void append(inout SimpsonsIntegrator inte, float value)
{
  inte.lastTerm = value;
  if (inte.count % 2 == 0)
  {
    inte.evens += value;
  }
  else
  {
    inte.odds += value;
  }
  
  inte.count++;
}

float integrate(inout SimpsonsIntegrator inte)
{
  // Note: sample count must always be even
  float stepSize = 1. / float(inte.count);
  float result = inte.firstTerm + inte.lastTerm;
  result += 4. * inte.odds;
  result += 2 * (inte.evens - inte.lastTerm);
  return result * stepSize / 3.;
}

void append(inout RectangleIntegrator inte, float value)
{
  inte.total += value;
  inte.count++;
}

float integrate(inout RectangleIntegrator inte)
{
  return inte.total;
}

#endif